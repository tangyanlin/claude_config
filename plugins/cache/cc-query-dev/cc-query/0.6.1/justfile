# Run type checking (JS)
typecheck:
    npm run typecheck

# Run tests (JS)
test:
    @test/test.sh

# Bump version (patch by default, or major|minor|patch)
bump type="patch":
    @scripts/bump.sh {{type}}

# === Rust (ccq) targets ===

duckdb_version := "v1.4.4"
duckdb_src := "duckdb-static/duckdb"
duckdb_lib := "duckdb-static/lib"
duckdb_include := "duckdb-static/include"

# Build ccq binary (requires DuckDB static lib - run setup-duckdb first if needed)
build:
    cd ccq && \
    DUCKDB_LIB_DIR="$(pwd)/../{{duckdb_lib}}" \
    DUCKDB_INCLUDE_DIR="$(pwd)/../{{duckdb_include}}" \
    DUCKDB_STATIC=1 \
    cargo build --release

# Typecheck Rust
check:
    cd ccq && cargo check

# Run Rust unit tests
test-rust:
    cd ccq && cargo test

# Run e2e tests against ccq binary
test-e2e: build
    CC_QUERY="./ccq/target/release/ccq" ./test/test.sh

# Full validation (JS + Rust unit + e2e)
test-all: test test-rust test-e2e zig-test

# Benchmark startup time
bench:
    @echo "=== Startup time ===" && \
    for i in 1 2 3; do \
        /usr/bin/time -f "%e sec" ./ccq/target/release/ccq -d test/fixtures <<< "SELECT 1;" 2>&1 | grep sec; \
    done
    @echo "=== Binary size ===" && ls -lh ./ccq/target/release/ccq
    @echo "=== Dependencies ===" && ldd ./ccq/target/release/ccq | grep -E "duckdb|not found" || echo "No DuckDB .so dependency (good!)"

# === DuckDB static library (one-time setup) ===

# Clone and build DuckDB static library with zig c++ (~10 min first time)
# Produces a libc++ ABI lib usable by both Zig and Rust
setup-duckdb:
    #!/usr/bin/env bash
    set -euo pipefail

    # Clone if needed
    if [ ! -d "{{duckdb_src}}" ]; then
        git clone -b {{duckdb_version}} --depth 1 https://github.com/duckdb/duckdb.git {{duckdb_src}}
    fi

    # Patch pcg_extras.hpp to remove __DATE__/__TIME__ (causes -Werror,-Wdate-time with clang/zig)
    pcg_file="{{duckdb_src}}/third_party/pcg/pcg_extras.hpp"
    if grep -q '__DATE__ __TIME__' "$pcg_file"; then
        sed -i 's/__DATE__ __TIME__ __FILE__/__FILE__/' "$pcg_file"
        echo "Patched pcg_extras.hpp (removed __DATE__ __TIME__)"
    fi

    # Build with zig c++ for libc++ ABI
    cd {{duckdb_src}}
    CC="zig cc" CXX="zig c++" \
    BUILD_EXTENSIONS='json' \
    ENABLE_EXTENSION_AUTOLOADING=0 \
    ENABLE_EXTENSION_AUTOINSTALL=0 \
    GEN=ninja \
    make release

    # bundle-library needs this dir to exist (even if empty)
    mkdir -p build/release/vcpkg_installed
    make bundle-library

    # Copy to output directories
    cd {{justfile_directory()}}
    mkdir -p {{duckdb_lib}} {{duckdb_include}}
    cp {{duckdb_src}}/build/release/libduckdb_bundle.a {{duckdb_lib}}/libduckdb_static.a
    ln -sf libduckdb_static.a {{duckdb_lib}}/libduckdb.a
    cp {{duckdb_src}}/src/include/duckdb.h {{duckdb_include}}/

    # Copy zig's libc++ and libc++abi so Rust can find them in the same lib dir
    tmp_cpp=$(mktemp --suffix=.cpp)
    tmp_out=$(mktemp)
    echo 'int main() {}' > "$tmp_cpp"
    zig_link_output=$(zig c++ -v "$tmp_cpp" -o "$tmp_out" 2>&1)
    rm -f "$tmp_cpp" "$tmp_out"
    for lib in libc++.a libc++abi.a; do
        path=$(echo "$zig_link_output" | tr ' ' '\n' | grep "/${lib}$" | head -1)
        if [ -n "$path" ]; then
            cp "$path" {{duckdb_lib}}/
        fi
    done
    echo "Copied zig's libc++.a and libc++abi.a to {{duckdb_lib}}/"

    echo "DuckDB static library ready in {{duckdb_lib}}/"

# Clean DuckDB build artifacts (keeps source)
clean-duckdb:
    rm -rf {{duckdb_src}}/build {{duckdb_lib}} {{duckdb_include}}

# Clean everything including DuckDB source
clean-all: clean-duckdb
    rm -rf {{duckdb_src}} ccq/target

# === Cross-compilation targets ===

# Download pre-built DuckDB libs (requires gh auth)
download-duckdb-libs:
    DUCKDB_LIBS_VERSION={{duckdb_version}} ./scripts/download-duckdb-libs.sh

# Build for a specific target (requires duckdb-libs)
build-target target:
    cd ccq && \
    DUCKDB_LIB_DIR="$(pwd)/../duckdb-libs/{{target}}" \
    DUCKDB_INCLUDE_DIR="$(pwd)/../duckdb-libs/{{target}}" \
    DUCKDB_STATIC=1 \
    cargo zigbuild --release --target {{target}}

# Test goreleaser config
goreleaser-check:
    goreleaser check

# Dry-run release (no publish)
release-dry:
    goreleaser release --snapshot --clean

# === Zig targets ===

# Build Zig version
zig-build:
    cd zig && zig build

# Run Zig tests
zig-test: zig-build
    @test/test-zig.sh

# Build Zig release version
zig-release:
    cd zig && zig build -Doptimize=ReleaseFast
